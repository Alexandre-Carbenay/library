buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'au.com.dius.pact.provider:gradle:4.6.16'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'au.com.dius.pact' version '4.6.16'
	id 'io.miret.etienne.sass' version '1.5.1'
}

group = 'org.adhuc.library'
version = '1'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

configurations {
	scss { transitive = false }
}

ext {
	springCloudVersion = '3.2.0'
	bootstrapVersion = '5.3.3'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.data:spring-data-commons'
	implementation "org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j:${springCloudVersion}"
	runtimeOnly "org.webjars.npm:bootstrap:${bootstrapVersion}"
	runtimeOnly 'org.webjars.npm:font-awesome:4.7.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'au.com.dius.pact.consumer:junit5:4.6.15'
	testImplementation 'org.aspectj:aspectjweaver:1.9.22.1'

	scss 'org.webjars.npm:bootstrap:5.3.3'
}

task extractBootstrap(type: Copy) {
	from zipTree(configurations.scss.singleFile).matching {
		include "META-INF/resources/webjars/bootstrap/${bootstrapVersion}/scss/**/*.scss"
	}
	into 'build/bootstrap'
}

task copyBootstrap(type: Copy) {
	dependsOn extractBootstrap
	from ("build/bootstrap/META-INF/resources/webjars/bootstrap/${bootstrapVersion}/scss") {
		include '**/*.scss'
	}
	into "${projectDir}/src/main/sass"
}

compileSass {
	dependsOn copyBootstrap
	sourceDir = project.file("${projectDir}/src/main/sass")
	outputDir = project.file("${projectDir}/src/main/resources/static/resources/css")
}

processResources {
	dependsOn compileSass
}

tasks.named('test') {
	useJUnitPlatform()
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--short', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def getGitBranch = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

pact {
	publish {
		pactDirectory = "${projectDir}/build/pacts"
		pactBrokerUrl = 'http://localhost:9292/'
		consumerBranch = getGitBranch()
		consumerVersion = getGitHash()
	}
}
